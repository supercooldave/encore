module Decode

import Data.Maybee hiding(map)
import Data.Either hiding(or_else)
import Collections.Mutable.HashMap
import Hash.HashCode
import JSON.Parser
import JSON.Raw

{- The design of this module follows Elm's -}

-- TODO: move to another module related to Exceptions 
-- But inside Encore, not Sahand's
typedef Result[t] = Either[String,t]

fun good[t](x : t) : Result[t]
  Right[String,t](x)
end

fun bad[t](s : String) : Result[t]
  Left[String,t](s)
end


{- decode :: (Decoder[t], String) -> Result[String,t]
   first parses the string into a JSON value
   and then runs a decoder on the result.
   Fails if either the parser or the decoder fails

     decode(new IntDecoder(), "4")      == Right 4
     decode(new IntDecoder(), "3 + 2")  == Left ...
     
  TODO: find an easier way of creating decoders
-}
fun decode[t](decoder : Decoder[t], str: String) : Result[t]
  match parse(str) with
    case Nothing => bad[t]("JSON parser failed.")
    case Just(raw) => decoder.decode(raw)
  end
end


-- A trait for JSON decoders
read trait Decoder[t]
  require def decode(raw: Value) : Result[t]
end

-- the decoders

{-  Decode JSON String into a String 

    decode(new StringDecoder(), "true")              == Left ...
    decode(new StringDecoder(), "42")                == Left ...
    decode(new StringDecoder(), "3.14")              == Left ...
    decode(new StringDecoder(), "\"hello\"")         == Right "hello"
    decode(new StringDecoder(), "{ \"hello\": 42 }") == Left ...
-}
class StringDecoder : Decoder[String]
  def decode(raw: Value) : Result[String]
    match raw with
      case RawString(str) => good(str) 
      case _ => bad[String]("Wrong format for JSON string")
    end
  end 
end

{- Decode a JSON number into an int

    decode(new IntDecoder(), "true")              == Left ...
    decode(new IntDecoder(), "42")                == Right 42
    decode(new IntDecoder(), "3.14")              == Left ...
    decode(new IntDecoder(), "\"hello\"")         == Left ...
    decode(new IntDecoder(), "{ \"hello\": 42 }") == Left ...
-}
class IntDecoder : Decoder[int]
  def decode(raw: Value) : Result[int]
    match raw with
      case RawNumber(num) =>
        match num.to_int() with
          case Just(i) => good(i)
          case Nothing => bad[int]("Wrong format for JSON int")
        end
      end
      case _ => bad[int]("Wrong format for JSON int")
    end
  end 
end


{- Decode a JSON boolean into a bool

    decode(new BoolDecoder(), "true")              == Right True
    decode(new BoolDecoder(), "42")                == Left ...
    decode(new BoolDecoder(), "3.14")              == Left ...
    decode(new BoolDecoder(), "\"hello\"")         == Left ...
    decode(new BoolDecoder(), "{ \"hello\": 42 }") == Left ...
-}
class BoolDecoder : Decoder[bool]
  def decode(raw: Value) : Result[bool]
    match raw with
      case RawBool(i) => good(i)
      case _ => bad[bool]("Wrong format for JSON bool")
    end
  end 
end

{- Decode a JSON number into a real

    decode(new RealDecoder(), "true")              == Left ..
    decode(new RealDecoder(), "42")                == Right 42
    decode(new RealDecoder(), "3.14")              == Right 3.14
    decode(new RealDecoder(), "\"hello\"")         == Left ...
    decode(new RealDecoder(), "{ \"hello\": 42 }") == Left ...
-}
class RealDecoder : Decoder[real]
  def decode(raw: Value) : Result[real]
    match raw with
      case RawNumber(num) =>
        match num.to_real() with
          case Just(i) => good(i)
          case Nothing => bad[real]("Wrong format for JSON real")
        end
      end
      case _ => bad[real]("Wrong format for JSON real")
    end
  end 
end

{- Decode a `null` value into some value.

    decode(new NullDecoder[bool](False), "null") == Right False
    decode(new NullDecoder[int](42), "null") == Right 42
    decode(new NullDecoder[int](42), "42") == Left ...
    decode(new NullDecoder[int](42), "false") == Left ...

-}
class NullDecoder[t] : Decoder[t](default)
  val default : t
  
  def init(default : t) : unit
    this.default = default
  end
  
  def decode(raw: Value) : Result[t]
    match raw with
      case Null() => good(this.default)
      case _ => bad[t]("Does not match JSON null")
    end
  end  
end


{- Decode a nullable JSON value into an Elm value.

    decode(new Nullable[int](new IntDecoder()), "42")   == Right (Just 42)
    decode(new Nullable[int](new IntDecoder()), "null") == Right Nothing
    decode(new Nullable[int](new IntDecoder()), "true") == Left ...
-}
class Nullable[t] : Decoder[Maybe[t]](dec)
  val dec : Decoder[Maybe[t]]
  
  def init(sub : Decoder[t]) : unit
    this.dec = one_of([ new NullDecoder[Maybe[t]](Nothing) : Decoder[Maybe[t]], 
                        map(fun (x : t) => Just(x), sub)]) 
  end

  def decode(raw: Value) : Result[Maybe[t]]
    this.dec.decode(raw)
  end
end


{- ignore the JSON and fail -}
class Fail[t] : Decoder[t](msg)
  val msg : String
  
  def init(msg : String) : unit
    this.msg = msg
  end

  def decode(raw: Value) : Result[t]
    bad[t](this.msg)
  end
end



{- Ignore the JSON and produce a certain value.  
    decode(new Succeed[int](42), "true")    == Right 42
    decode(new Succeed[int](42), "[1,2,3]") == Right 42
    decode(new Succeed[int](42), "Hello")   == Left ... -- this is not a valid JSON string
-}
class Succeed[t] : Decoder[t](default)
  val default : t
  
  def init(default : t) : unit
    this.default = default
  end
  
  def decode(raw: Value) : Result[t]
    good(this.default)
  end  
end

class DecoderHolder[t] : Decoder[t](dec)
  val dec : Value -> Result[t]
  
  def init(dec : Value -> Result[t]) : unit
    this.dec = dec
  end

  def decode(raw: Value) : Result[t]
    this.dec(raw)
  end
end



{- Extract a particular field from a JSON object

    decode(new Field[int]("x", new IntDecoder()), "{ \"x\": 3 }")                == Right 3
    decode(new Field[int]("x", new IntDecoder()), "{ \"x\": 3, \"y\": 4 }")      == Right 3
    decode(new Field[int]("x", new IntDecoder()), "{ \"x\": true }")             == Left ...
    decode(new Field[int]("x", new IntDecoder()), "{ \"y\": 4 }")                == Left ...
    decode(new Field[String]("name", new StringDecoder()), "{ \"name\": \"tom\" }") == Right "tom"
-}
class Field[t] : Decoder[t](name, sub)
  val name : String
  val sub : Decoder[t]
  
  def init(name : String, sub : Decoder[t]) : unit
    this.name = name
    this.sub = sub
  end
  
  def decode(raw: Value) : Result[t]
    match raw with
      case RawObject(hash) => 
        match hash.get_value(this.name) with
          case Just(value) => this.sub.decode(value)
          case Nothing => bad[t]("Expected a JSON object with field '".concatenate(this.name).concatenate("'"))
        end
      end
      case _ => bad[t]("Expected a JSON object")
    end
  end
end


{- Extract a particular path from a JSON object

    json = "{ "person": { "name": "tom", "age": 42 } }"

    decodeString (at ["person", "name"] string) json  == Ok "tom"
    decodeString (at ["person", "age" ] int   ) json  == Ok "42

This is really just a shorthand for saying things like:

    field "person" (field "name" string) == at ["person","name"] string
-}
class At[t] : Decoder[t](sub, path)
  val path : [String]
  val sub : Decoder[t]
  
  def init(path : [String], sub : Decoder[t]) : unit
    this.path = path
    this.sub = sub
  end

  def decode(raw : Value) : Result[t]
    var node = raw
    var decoder = this.sub
    for name <- this.path do
      match dig(name, node) with
        case Just(value) => 
          node = value 
          bad[t]("nothing") -- value ignored, used to help type checker
        end
        case Nothing => return bad[t]("Expect path ".concatenate(".".join(this.path)))
      end
    end    
    decoder.decode(node)
  where
    fun dig(name : String, node : Value) : Maybe[Value]
      match node with
        case RawObject(hash) => hash.get_value(name)
        case _ => Nothing
      end
    end
  end
end


{- extract given index of JSON array
TODO: fix comment
    json = """[ "alice", "bob", "chuck" ]"""

    decodeString (index 0 string) json  == Ok "alice"
    decodeString (index 1 string) json  == Ok "bob"
    decodeString (index 2 string) json  == Ok "chuck"
    decodeString (index 3 string) json  == Err ...
-}
class Index[t] : Decoder[t](index, dec)
  val index : int
  val dec : Decoder[t]
  
  def init(index : int, dec : Decoder[t]) : unit
    this.index = index
    this.dec = dec
  end
  
  def decode(raw : Value) : Result[t]
    match raw with
      case RawArray(arr) => 
        if this.index >= 0 && this.index < |arr| then
          this.dec.decode(arr(this.index))
        else
          bad[t]("Index out of bounds")
        end
      end
      case _ => bad[t]("Expected JSON array")
    end
  end
end


{- Decode a JSON array

    decode(new ArrayDecoder[int](new IntDecoder()), "[1,2,3]")      == Right [1,2,3]
    decode(new ArrayDecoder[int](new IntDecoder()), "[true,false]") == Right [true,false] 
-}
class ArrayDecoder[t] : Decoder[[t]](dec)
  val dec : Decoder[t]
  
  def init(dec : Decoder[t]) : unit
    this.dec = dec
  end
  
  def decode(raw: Value) : Result[[t]]
    match raw with
      case RawArray(arr) => 
        val res = new [t](|arr|)
        for i <- [0 .. |arr| - 1] do
          match this.dec.decode(arr(i)) with
            case Right(v) => 
              res(i) = v
              bad[[t]]("Nothing") -- a hack to please type system
            end
            case Left(err) => return bad[[t]](err)
          end
        end
        good(res)
      end
      case _ => bad[[t]]("Expected JSON array")
    end
  end
end



{- Decode a JSON object into a hash table

    decode(new Dict[t](new IntDecoder()), "{ \"alice\": 42, \"bob\": 99 }")
      == HashMap["alice" => 42, "bob" => 99]
-}
class Dict[t] : Decoder[HashMap[t]](dec)
  val dec : Decoder[t]
  
  def init(dec : Decoder[t]) : unit
    this.dec = dec
  end
  
  def decode(raw : Value) : Result[HashMap[t]]
    match raw with
      case RawObject(hash) => 
        val iter = hash.iterator()
        val new_map = new HashMap[t]()
        while iter.has_next() do
          val x = unjust(iter.next())
          match this.dec.decode(x.value) with
            case Right(y) => 
              new_map.set(x.key, y)
              bad[HashMap[t]]("Nothing")  -- HACK to please type checker
            end
            case Left(err) => return bad[HashMap[t]](err)
          end
        end

        return good(new_map)
      end
      case _ => bad[HashMap[t]]("Expected JSON object")
    end
  end
end


-- TODO: Fix so that it returns list of strings, once HashMap has been
-- genericied
{- Decode a JSON object into an array of pairs.

    decode(keyValuePairs(new IntDecoder()), "{ \"alice\": 42, \"bob\": 99 }")
      == [("alice", 42), ("bob", 99)]
-}
{- TODO: FIX: NOT TYPE CHECKING
fun key_value_pairs[t](dec : Decoder[t]) : Decoder[(HashCode, t)]
  map(fun(x : HashMap[t]) => x.key_value_pairs(), new Dict(dec) : Decoder[HashMap[t]])
end
-}



-- utilities
fun one_of[t](decs : [Decoder[t]]) : Decoder[t]
  one_of_helper(decs,0)
where
  fun one_of_helper(decs : [Decoder[t]], index : int) : Decoder[t]
    if index < |decs| then
      or_else(decs(index), one_of_helper(decs, index + 1))
    else
      new Fail[t]("No valid choice")
    end
  end
end

-- TODO: comment (basically, the core of one_of)
-- should be local to one_of
fun or_else[t](dec1 : Decoder[t], dec2 : Decoder[t]) : Decoder[t]
  val next = fun (str : Value)
               val one = dec1.decode(str)
               if is_right(one) then
                 one
               else
                 dec2.decode(str)
               end
             end
  new DecoderHolder[t](next)
end



{- TODO: completely rewrite comment, as it is terrible

 Helpful for dealing with optional fields. Here are a few slightly different
examples:

    json = """{ "name": "tom", "age": 42 }"""

    decodeString (maybe (field "age"    int  )) json == Ok (Just 42)
    decodeString (maybe (field "name"   int  )) json == Ok Nothing
    decodeString (maybe (field "height" float)) json == Ok Nothing

    decodeString (field "age"    (maybe int  )) json == Ok (Just 42)
    decodeString (field "name"   (maybe int  )) json == Ok Nothing
    decodeString (field "height" (maybe float)) json == Err ...

Notice the last example! It is saying we *must* have a field named `height` and
the content *may* be a float. There is no `height` field, so the decoder fails.

Point is, `maybe` will make exactly what it contains conditional. For optional
fields, this means you probably want it *outside* a use of `field` or `at`.
-}
class MaybeDecoder[t] : Decoder[Maybe[t]](dec)
  val dec : Decoder[t]
  
  def init(dec : Decoder[t]) : unit
    this.dec = dec
  end

  def decode(raw: Value) : Result[Maybe[t]]
    match this.dec.decode(raw) with
      case Right(value) => good(Just(value))
      case Left(err) => good(Nothing : Maybe[t])
    end
  end
end

{- TODO : FIX Do not do anything with a JSON value, just bring it into Elm as a `Value`.
This can be useful if you have particularly crazy data that you would like to
deal with later. Or if you are going to send it out a port and do not care
about its structure.
-}

class ValueDecoder : Decoder[Value]
  def decode(value : Value) : Result[Value]
    good(value)
  end
end


-- mapping functions
fun map[a,v](f : a -> v, dec : Decoder[a]) : Decoder[v]
  new Map[a,v](f, dec)
end

class Map[a,v] : Decoder[v](dec,f)
  val dec : Decoder[a]
  val f : a -> v
  
  def init(f : a -> v, dec : Decoder[a]) : unit
    this.f = f
    this.dec = dec
  end
  
  def decode(raw : Value) : Result[v]
    this.dec.decode(raw).map(this.f) 
  end
end

fun map2[a,b,v](f : (a,b) -> v, dec1 : Decoder[a], dec2 : Decoder[b]) : Decoder[v]
  new Map2[a,b,v](f, dec1, dec2)
end

class Map2[a,b,v] : Decoder[v](dec1,dec2,f)
  val dec1 : Decoder[a]
  val dec2 : Decoder[b]
  val f : (a,b) -> v
  
  def init(f : (a,b) -> v, dec1 : Decoder[a], dec2 : Decoder[b]) : unit
    this.f = f
    this.dec1 = dec1
    this.dec2 = dec2
  end
  
  def decode(raw : Value) : Result[v]
    match this.dec1.decode(raw) with
      case Left(err) => bad[v](err)
      case Right(res1) =>
        match this.dec2.decode(raw) with
          case Left(err) => bad[v](err)
          case Right(res2) => good(this.f(res1, res2))
        end
      end
    end    
  end
end


fun map3[a,b,c,v](f : (a,b,c) -> v, dec1 : Decoder[a], dec2 : Decoder[b], dec3 : Decoder[c]) : Decoder[v]
  new Map3[a,b,c,v](f, dec1, dec2, dec3)
end

class Map3[a,b,c,v] : Decoder[v](dec1,dec2,dec3,f)
  val dec1 : Decoder[a]
  val dec2 : Decoder[b]
  val dec3 : Decoder[c]
  val f : (a,b,c) -> v
  
  def init(f : (a,b,c) -> v, dec1 : Decoder[a], dec2 : Decoder[b], dec3 : Decoder[c]) : unit
    this.f = f
    this.dec1 = dec1
    this.dec2 = dec2
    this.dec3 = dec3    
  end
  
  def decode(raw : Value) : Result[v]
    match this.dec1.decode(raw) with
      case Left(err) => bad[v](err)
      case Right(res1) =>
        match this.dec2.decode(raw) with
          case Left(err) => bad[v](err)
          case Right(res2) => 
            match this.dec3.decode(raw) with
              case Left(err) => bad[v](err)
              case Right(res3) => good(this.f(res1, res2, res3))
            end
          end
        end
      end
    end    
  end
end


--- TODO: UP TO 8 ---


-- others
class AndThen[a,b] : Decoder[b](dec,f)
  val f : a -> Decoder[b]
  val dec : Decoder[a]
  
  def init(f : a -> Decoder[b], dec : Decoder[a]) : unit
    this.f = f
    this.dec = dec
  end
  
  def decode(raw : Value) : Result[b]
    match this.dec.decode(raw) with
      case Right(value) => this.f(value).decode(raw)
      case Left(err) => bad[b](err)
    end 
  end
end


fun lazy[a](thunk : unit -> Decoder[a]) : Decoder[a]
  new AndThen[unit,a](thunk, new Succeed[unit](()))
end

